/-
Copyright (c) 2026 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.Normed.Module.RCLike.Basic
import Mathlib.Analysis.Normed.Module.RieszLemma
import Mathlib.Analysis.Normed.Operator.Banach
import Mathlib.Analysis.Normed.Operator.BoundedLinearMaps
import Mathlib.Analysis.Normed.Operator.Compact
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Instances
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order


/-!
# Spectral theory of compact operators

This file develops the spectral theory of compact operators on Banach spaces.
The main result is the Fredholm alternative for compact operators.

## Main results

* `antilipschitz_of_not_hasEigenvalue`: if `T` is a compact operator and `Î¼ â‰  0` is not an
  eigenvalue, then `T - Î¼I` is antilipschitz.
* `fredholm_alternative`: the Fredholm alternative for compact operators.

We follow https://terrytao.wordpress.com/2011/04/10/a-proof-of-the-fredholm-alternative/
-/

-- let X be a Banach space
variable {ğ•œ X : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup X] [NormedSpace ğ•œ X]
-- and T be a compact operator on it
variable {T : X â†’L[ğ•œ] X}

open Module End

/-- If a continuous linear map `f` satisfies `â€–xâ€– = 1 â†’ 1 â‰¤ K * â€–f xâ€–`, then `f` is
antilipschitz with constant `K`. -/
lemma ContinuousLinearMap.antilipschitz_of_bound_of_norm_one {ğ•œ : Type*} [RCLike ğ•œ] {X Y : Type*}
    [NormedAddCommGroup X] [NormedSpace ğ•œ X] [NormedAddCommGroup Y] [NormedSpace ğ•œ Y]
    (f : X â†’L[ğ•œ] Y) {K : NNReal} (h : âˆ€ x, â€–xâ€– = 1 â†’ 1 â‰¤ K * â€–f xâ€–) :
    AntilipschitzWith K f :=
  ContinuousLinearMap.antilipschitz_of_bound _ fun x â†¦ by
    obtain rfl | hx := eq_or_ne x 0
    Â· simp
    simpa [norm_smul, field] using h ((â€–xâ€–â»Â¹ : ğ•œ) â€¢ x) (norm_smul_inv_norm hx)

open Filter Topology in
/-- If `T : X â†’L[ğ•œ] X` is a compact operator on a Banach space `X`, and `Î¼ â‰  0` is not an
eigenvalue of `T`, then `T - Î¼ â€¢ 1` is antilipschitz with positive constant.
That is, `T - Î¼ â€¢ 1` is bounded below as an operator.

This is a useful step in the proof of the Fredholm alternative. for compact operators. -/
theorem antilipschitz_of_not_hasEigenvalue (hT : IsCompactOperator T)
    {Î¼ : ğ•œ} (hÎ¼ : Î¼ â‰  0) (h : Â¬ HasEigenvalue (T : End ğ•œ X) Î¼) :
    âˆƒ K > 0, AntilipschitzWith K (T - Î¼ â€¢ 1 : X â†’L[ğ•œ] X) := by
  -- Suppose not, then for every K > 0, there is some x such that â€–(T - Î¼ â€¢ 1) xâ€– < K * â€–xâ€–.
  by_contra! hK
  replace hK : âˆ€ K > 0, âˆƒ x, â€–(T - Î¼ â€¢ 1) xâ€– < K * â€–xâ€– := by
    contrapose! hK
    obtain âŸ¨K, hKâ‚€, hKâŸ© := hK
    refine âŸ¨K.toNNRealâ»Â¹, by positivity, ?_âŸ©
    apply AddMonoidHomClass.antilipschitz_of_bound
    simpa [NNReal.coe_inv, le_inv_mul_iffâ‚€, hKâ‚€, hKâ‚€.le] using hK
  -- In fact, there is a lower bound `c` such that for every Îµ > 0, there is an `x` with norm
  -- in the interval `[c, 1]` such that `â€–(T - Î¼ â€¢ 1) xâ€– < Îµ`.
  -- (In the case of an RCLike field, where we can rescale, we could even get `â€–xâ€– = 1`, but we
  -- don't need that.)
  replace hK : âˆƒ c > 0, âˆ€ Îµ > 0, âˆƒ x, â€–xâ€– â‰¤ 1 âˆ§ c â‰¤ â€–xâ€– âˆ§ â€–(T - Î¼ â€¢ 1) xâ€– < Îµ := by
    obtain âŸ¨C, hCâŸ© := NormedField.exists_one_lt_norm ğ•œ
    refine âŸ¨â€–Câ€–â»Â¹, by positivity, fun Îµ hÎµ â†¦ ?_âŸ©
    obtain âŸ¨x, hxâŸ© := hK Îµ (by positivity)
    have : x â‰  0 := by aesop
    obtain âŸ¨Î·, hÎ·, hâ‚, hâ‚‚, hâ‚ƒâŸ© := rescale_to_shell hC (Îµ := 1) (by simp) this
    refine âŸ¨Î· â€¢ x, hâ‚.le, by simpa using hâ‚‚, ?_âŸ©
    grw [map_smul, norm_smul, hx, mul_left_comm, â† norm_smul]
    linear_combination Îµ * hâ‚
  obtain âŸ¨c, hcâ‚€, hcâŸ© := hK
  obtain âŸ¨Ï†, hÏ†_anti, hÏ†_pos, hÏ†âŸ© := exists_seq_strictAnti_tendsto (0 : â„)
  -- Then find a sequence of vectors `xâ‚™` with norm in the interval `[c, 1]` such
  -- that `â€–(T - Î¼ â€¢ 1) xâ‚™â€– < Ï† n`, where `Ï† n` is a sequence of positive numbers tending to zero.
  have (n : â„•) : âˆƒ x, â€–xâ€– â‰¤ 1 âˆ§ c â‰¤ â€–xâ€– âˆ§ â€–(T - Î¼ â€¢ 1) xâ€– < Ï† n := hc (Ï† n) (hÏ†_pos n)
  choose x hx_norm_upper hx_norm_lower hx_bound using this
  have hx_lim : Tendsto (fun n â†¦ (T - Î¼ â€¢ 1) (x n)) atTop (ğ“ 0) := squeeze_zero_norm (by grind) hÏ†
  -- Define the sequence of vectors yâ‚™ := T xâ‚™
  let y_ (n : â„•) : X := T (x n)
  -- which are bounded away from zero.
  have hy_lower : âˆƒ d > 0, âˆ€á¶  n in atTop, d â‰¤ â€–y_ nâ€– := by
    refine âŸ¨(â€–Î¼â€– * c) / 2, by positivity, ?_âŸ©
    filter_upwards [hÏ†.eventually_le_const (show (â€–Î¼â€– * c) / 2 > 0 by positivity)] with n hn
    have hâ‚ : â€–T (x n) - Î¼ â€¢ x nâ€– < Ï† n := by simpa using hx_bound n
    have hâ‚‚ : â€–Î¼â€– * â€–x nâ€– â‰¤ â€–T (x n)â€– + â€–T (x n) - Î¼ â€¢ x nâ€– := by
      simpa [norm_smul] using norm_le_norm_add_norm_sub (T (x n)) (Î¼ â€¢ x n)
    linear_combination hâ‚‚ + hâ‚ + hn + â€–Î¼â€– * hx_norm_lower n
 -- The sequence yâ‚™ is contained in the image of the closed unit ball under T, which is compact,
  -- since T is, so we can extract a convergent subsequence, and say y_ (Ïˆ n) â†’ y.
  obtain âŸ¨K, hK, hK'âŸ© := hT.image_closedBall_subset_compact 1
  obtain âŸ¨y, hyK, Ïˆ, hÏˆ, hÏˆyâŸ© := hK.tendsto_subseq (x := y_) (fun n â†¦ hK' âŸ¨x n, by simp [*], rflâŸ©)
  -- However (T - Î¼ â€¢ 1) yâ‚™ = T ((T - Î¼ â€¢ 1) xâ‚™) â†’ 0
  have hy_lim : Tendsto (fun n â†¦ (T - Î¼ â€¢ 1) (y_ n)) atTop (nhds 0) := by
    have : Tendsto (fun n â†¦ _) _ _ := T.continuous.continuousAt.tendsto.comp hx_lim
    simpa using this
  -- so (T - Î¼ â€¢ 1) y = 0.
  have hy_eigen' : (T - Î¼ â€¢ 1) y = 0 := by
    apply tendsto_nhds_unique _ (hy_lim.comp hÏˆ.tendsto_atTop)
    have : Continuous (T - Î¼ â€¢ 1 : X â†’L[ğ•œ] X) := by fun_prop
    exact this.continuousAt.tendsto.comp hÏˆy
  -- Since yâ‚™ are bounded away from 0, we must have y â‰  0.
  have hy_ne : y â‰  0 := by
    obtain âŸ¨d, hdâ‚€, hdâŸ© := hy_lower
    rintro rfl
    suffices âˆ€á¶  n : â„• in atTop, False by rwa [eventually_const] at this
    rw [NormedAddCommGroup.tendsto_nhds_zero] at hÏˆy
    filter_upwards [hÏˆ.tendsto_atTop.eventually hd, hÏˆy d (by positivity)] using by grind
  -- So y is an eigenvector of T with eigenvalue Î¼,
  have : HasEigenvector (T : End ğ•œ X) Î¼ y := by
    simpa [hasEigenvector_iff, mem_genEigenspace_one, hy_ne, sub_eq_zero] using hy_eigen'
  -- which is a contradiction.
  exact h (hasEigenvalue_of_hasEigenvector this)

/-- A variation of Riesz's lemma for where we get a vector `xâ‚€` of norm exactly 1. -/
theorem riesz_lemma_one
    {ğ•œ : Type*} [RCLike ğ•œ] {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {F : Subspace ğ•œ E} (hFc : IsClosed (F : Set E)) (hF : âˆƒ (x : E), x âˆ‰ F) {r : â„} (hr : r < 1) :
    âˆƒ xâ‚€ âˆ‰ F, â€–xâ‚€â€– = 1 âˆ§ âˆ€ y âˆˆ F, r â‰¤ â€–xâ‚€ - yâ€– := by
  obtain âŸ¨xâ‚€, hxâ‚€, hâŸ© := riesz_lemma hFc hF hr
  have hxâ‚€' : xâ‚€ â‰  0 := by rintro rfl; simp at hxâ‚€
  refine âŸ¨(â€–xâ‚€â€–â»Â¹ : ğ•œ) â€¢ xâ‚€, ?_, norm_smul_inv_norm hxâ‚€', ?_âŸ©
  Â· rwa [Submodule.smul_mem_iff]
    simpa
  intro y hy
  have hâ‚‚ : â€–(â€–xâ‚€â€– : ğ•œ)â»Â¹ â€¢ (xâ‚€ - (â€–xâ‚€â€– : ğ•œ) â€¢ y)â€– = â€–xâ‚€â€–â»Â¹ * â€–xâ‚€ - (â€–xâ‚€â€– : ğ•œ) â€¢ yâ€– := by
    rw [norm_smul, norm_inv, norm_algebraMap', norm_norm]
  have hâ‚ := h ((â€–xâ‚€â€– : ğ•œ) â€¢ y) (F.smul_mem _ hy)
  rwa [â† le_inv_mul_iffâ‚€' (by simpa), â† hâ‚‚, smul_sub, inv_smul_smulâ‚€] at hâ‚
  simpa using hxâ‚€'

/--
Given an endomorphism `S` of a normed space that's a closed embedding but not surjective, we can
find a sequence of vectors `f n`, living inside a shell, such that `f n` is in the
range of `S ^ n` but is at least `1` away from any vector in the range of `S ^ (n + 1)`.
This is a useful construction for the proof of the Fredholm alternative for compact operators.
-/
theorem exists_seq {ğ•œ X : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup X] [NormedSpace ğ•œ X]
    {S : End ğ•œ X} (hS_not_surj : Â¬ (S : X â†’ X).Surjective)
    (hS_anti : Topology.IsClosedEmbedding S)
    {c : ğ•œ} (hc : 1 < â€–câ€–) {R : â„} (hR : â€–câ€– < R) :
    âˆƒ f : â„• â†’ X,
      (âˆ€ n, 1 â‰¤ â€–f nâ€–) âˆ§ (âˆ€ n, â€–f nâ€– â‰¤ R) âˆ§ (âˆ€ n, f n âˆˆ (S ^ n).range) âˆ§
      (âˆ€ n, âˆ€ y âˆˆ (S ^ (n + 1)).range, 1 â‰¤ â€–f n - yâ€–) := by
  obtain âŸ¨x, hxâŸ© : âˆƒ x : X, âˆ€ y, S y â‰  x := by simpa [Function.Surjective] using hS_not_surj
  let V (n : â„•) : Submodule ğ•œ X := S.iterateRange n
  have hV_succ (n : â„•) : V (n + 1) = (V n).map (S : End ğ•œ X) := LinearMap.iterateRange_succ
  have hV_closed (n : â„•) : IsClosed (V n : Set X) := by
    induction n with
    | zero => simp [V, Module.End.one_eq_id]
    | succ n ih =>
      rw [hV_succ]
      apply hS_anti.isClosedMap _ ih
  have x (n : â„•) : âˆƒ x âˆˆ V n, 1 â‰¤ â€–xâ€– âˆ§ â€–xâ€– â‰¤ R âˆ§ âˆ€ y âˆˆ V (n + 1), 1 â‰¤ â€–x - yâ€– := by
    have hâ‚ : IsClosed (Submodule.comap (V n).subtype (V (n + 1)) : Set (V n)) := by
      simpa using (hV_closed (n + 1)).preimage_val
    have hâ‚‚ : âˆƒ x : V n, x âˆ‰ (V (n + 1)).comap (V n).subtype := by
      simpa [iterate_succ, V, (iterate_injective hS_anti.injective n).eq_iff] using by use x
    obtain âŸ¨âŸ¨x, hxâŸ©, hxn, hxyâŸ© := riesz_lemma_of_norm_lt hc hR hâ‚ hâ‚‚
    simp only [Submodule.mem_comap, Submodule.subtype_apply, AddSubgroupClass.coe_norm,
      AddSubgroupClass.coe_sub, Subtype.forall] at hxn hxy
    exact âŸ¨x, hx, (by simpa using hxy 0), hxn,
      fun y hy â†¦ hxy y (S.iterateRange.monotone (by simp) hy) hyâŸ©
  choose x hxv hxn hxn' hxy using x
  exact âŸ¨x, hxn, hxn', hxv, hxyâŸ©

/-- The Fredholm alternative for compact operators: if `T` is a compact operator and `Î¼ â‰  0`,
then either `Î¼` is an eigenvalue of `T`, or `Î¼` is in the resolvent set of `T`. -/
theorem fredholm_alternative {ğ•œ X : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup X] [NormedSpace ğ•œ X]
    [CompleteSpace X] {T : X â†’L[ğ•œ] X} (hT : IsCompactOperator T)
    {Î¼ : ğ•œ} (hÎ¼ : Î¼ â‰  0) : HasEigenvalue (T : End ğ•œ X) Î¼ âˆ¨ Î¼ âˆˆ resolventSet ğ•œ T := by
  by_contra!
  obtain âŸ¨hâ‚, hâ‚‚âŸ© := this
  let (eq := hS) S := (T - Î¼ â€¢ 1)
  replace hâ‚‚ : Â¬ (S : X â†’ X).Bijective := by
    rw [spectrum.mem_resolventSet_iff, â† IsUnit.neg_iff,
      ContinuousLinearMap.isUnit_iff_bijective] at hâ‚‚
    convert hâ‚‚
    ext x
    simp [S]
  obtain âŸ¨K, -, hK : AntilipschitzWith K SâŸ© := antilipschitz_of_not_hasEigenvalue hT hÎ¼ hâ‚
  obtain âŸ¨c, hcâŸ© := NormedField.exists_one_lt_norm ğ•œ
  obtain âŸ¨f, hf_norm_lower, hf_norm_upper, hf_mem, hf_farâŸ© :=
    exists_seq (mt (.intro hK.injective) hâ‚‚)
    (hK.isClosedEmbedding S.uniformContinuous) (c := c) hc (R := â€–câ€– + 1) (by simp)
  have hf_mem' (n : â„•) : S (f n) âˆˆ ((S : End ğ•œ X) ^ (n + 1)).range := by
    rw [iterate_succ']
    rw [LinearMap.range_comp]
    exact âŸ¨f n, hf_mem n, rflâŸ©
  have hp : Pairwise fun xâ‚ xâ‚‚ â†¦ â€–Î¼â€– â‰¤ â€–T (f xâ‚) - T (f xâ‚‚)â€– := by
    intro m n hmn
    wlog! hmn' : m < n generalizing m n
    Â· rw [norm_sub_rev]
      exact this hmn.symm (by order)
    let u : X := Î¼â»Â¹ â€¢ (S (f n) - S (f m) + Î¼ â€¢ f n)
    have hu : Î¼ â€¢ (f m - u) = (T (f m) - T (f n)) := by
      rw [smul_sub, smul_inv_smulâ‚€ hÎ¼]
      simp [S]
      linear_combination (norm := module)
    have : u âˆˆ ((S : End ğ•œ X) ^ (m + 1)).range := by
      apply Submodule.smul_mem _ _ (Submodule.add_mem _ _ _)
      Â· exact Submodule.sub_mem _ ((S : End ğ•œ X).iterateRange.monotone (by lia) (hf_mem' _))
          (hf_mem' _)
      Â· exact Submodule.smul_mem _ Î¼ ((S : End ğ•œ X).iterateRange.monotone (by lia) (hf_mem n))
    rw [â† hu, norm_smul, mul_comm]
    grw [â† hf_far _ u this, one_mul]
  obtain âŸ¨K, hK, hK'âŸ© := hT.image_closedBall_subset_compact (â€–câ€– + 1)
  obtain âŸ¨y, hyK, Ïˆ, hÏˆ, hÏˆyâŸ© := hK.tendsto_subseq (fun n â†¦ hK' âŸ¨f n, by simp [*], rflâŸ©)
  replace hÏˆy := hÏˆy.cauchySeq
  rw [Metric.cauchySeq_iff'] at hÏˆy
  obtain âŸ¨N, hNâŸ© := hÏˆy â€–Î¼â€– (by positivity)
  simp only [dist_eq_norm_sub, ContinuousLinearMap.coe_coe, Function.comp_apply] at hN
  have := hN (N + 1) (by simp)
  refine this.not_ge ?_
  apply hp
  simp [hÏˆ.injective.eq_iff]

theorem ContinuousLinearMap.isUnit_toLinearMap_iff [CompleteSpace X] {T : X â†’L[ğ•œ] X} :
    IsUnit T â†” IsUnit (T : End ğ•œ X) := by
  rw [ContinuousLinearMap.isUnit_iff_bijective, Module.End.isUnit_iff]
  rfl

theorem ContinuousLinearMap.spectrum_eq [CompleteSpace X] :
    spectrum ğ•œ (T : X â†’L[ğ•œ] X) = spectrum ğ•œ (T : End ğ•œ X) := by
  ext Î¼
  rw [spectrum, resolventSet, Set.mem_compl_iff, Set.mem_setOf,
    ContinuousLinearMap.isUnit_toLinearMap_iff]
  rfl

theorem hasEigenvalue_iff_mem_spectrum [CompleteSpace X] (hT : IsCompactOperator T)
    {Î¼ : ğ•œ} (hÎ¼ : Î¼ â‰  0) :
    HasEigenvalue (T : End ğ•œ X) Î¼ â†” Î¼ âˆˆ spectrum ğ•œ T := by
  constructor
  Â· intro hÎ¼'
    rw [ContinuousLinearMap.spectrum_eq]
    exact hÎ¼'.mem_spectrum
  Â· intro h
    exact (fredholm_alternative hT hÎ¼).resolve_right h
